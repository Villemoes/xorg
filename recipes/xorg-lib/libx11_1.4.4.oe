require libx11.inc
inherit gettext

RECIPE_TYPES = "sdk machine native"

EXTRA_OECONF += "--with-keysymdefdir=${HOST_SYSROOT}${includedir}/X11"

#removed xtrans - removed xslt support elsewhere too
DEPENDS += "xtrans util-macros libxdmcp libxau \
            bigreqsproto xproto xextproto xcmiscproto \
            xf86bigfontproto kbproto inputproto libxcb"
DEPENDS_${PN} += "xproto libxcb libxau libxdmcp libdl"
# "libxau libxdmcp"
RDEPENDS_${PN} +=  "libxcb libxau libxdmcp libdl libc"

SRC_URI += " file://keysymdef_include.patch \
             file://x11_disable_makekeys.patch \
             file://0001-Add-_XGetRequest-as-substitute-for-GetReq-GetReqExtr.patch \
             file://makekeys_crosscompile.patch \
             file://pkgconfig.patch \
             "

do_compile[prefuncs] += "do_compile_makekeys"
do_compile_makekeys() {
	cd ${S}/src/util
	mv makekeys.c.orig makekeys.c || true
	touch makekeys-makekeys.o
	(
		unset CC LD CXX CCLD CFLAGS CPPFLAGS LDFLAGS CXXFLAGS
		# MIN_REHASH 10 is only in 1.0.1
		sed -i -e 's:MIN_REHASH 10:MIN_REHASH 16:g' makekeys.c
		sed -i -e 's:MIN_REHASH 15:MIN_REHASH 16:g' makekeys.c
		touch makekeys-makekeys.o;
		if [ "${BUILD_WORDSIZE}" == "64" ]; then
			${BUILD_CC} ${BUILD_CFLAGS} -I${HOST_SYSROOT}${includedir} makekeys.c -o makekeys
		else
			${BUILD_CC} ${BUILD_CFLAGS} -I${HOST_SYSROOT}${includedir} -DUSE32 makekeys.c -o makekeys
		fi
	)
	if [ "$?" != "0" ]; then
		exit 1
	fi
	# mv to stop it getting rebuilt
	mv makekeys.c makekeys.c.orig
	cd ../../
}


